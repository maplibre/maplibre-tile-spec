plugins {
    id 'java-library'
    id('com.diffplug.spotless') version '8.2.1'
    id 'jacoco'
    id 'com.gradleup.shadow' version '9.3.1'
}

repositories {
    mavenCentral()
    maven {
        url = 'https://maven.ecc.no/releases'
    }
}

dependencies {
    // Core library dependency
    implementation project(':mlt-core')

    // CLI-specific dependencies
    implementation 'commons-cli:commons-cli:1.11.0'
    implementation 'org.apache.commons:commons-compress:1.28.0'
    implementation 'org.imintel:mbtiles4j:1.0.6'
    implementation 'org.xerial:sqlite-jdbc:3.51.2.0'

    // Additional dependencies needed by CLI classes
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'com.google.protobuf:protobuf-java:4.33.5'
    implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
    implementation 'org.jetbrains:annotations:26.0.2-1'
    implementation 'org.locationtech.jts:jts-core:1.20.0'
    implementation 'org.locationtech.jts.io:jts-io-common:1.20.0'

    // pmtiles support
    implementation platform('io.tileverse:tileverse-bom:1.2.0')
    implementation 'io.tileverse.rangereader:tileverse-rangereader-all'
    implementation 'io.tileverse.pmtiles:tileverse-pmtiles'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:6.0.2'
}

test {
    useJUnitPlatform()
    testLogging {
        outputs.upToDateWhen { false }
        showStandardStreams = true
        events "passed", "skipped", "failed"
        exceptionFormat = "full"
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = false
    }
    dependsOn test
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

spotless {
    groovy {
        target 'build.gradle'
        leadingTabsToSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()

    }
    java {
        importOrder()
        target 'src/*/java/**/*.java'
        googleJavaFormat()
        removeUnusedImports()
    }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs << "-Xlint:unchecked"
            options.compilerArgs << "-Xlint:deprecation"
            options.compilerArgs << "-Xlint:all"
        }
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

// Create CLI JARs
["encode", "decode"].each { name ->
    task "$name"(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar, dependsOn: [compileJava, ':mlt-core:jar']) {
        archiveFileName = name + ".jar"
        archiveClassifier = "cli"
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations = project.configurations.named('runtimeClasspath').map { [it] }

        // Filter out dependency JAR signatures to prevent `SecurityException: Invalid signature file digest for Manifest main attributes`
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'

        // Tileverse relies on the contents of META-INF/services to register service providers.
        // By default, these are ignored when building a fat JAR, so we need to merge them.
        filesMatching('META-INF/services/**') {
            duplicatesStrategy = DuplicatesStrategy.INCLUDE
        }
        mergeServiceFiles()

        manifest.from jar.manifest
        with jar
    }
}

encode.manifest.attributes 'Implementation-Title': 'encode an mlt from an mvt',
        'Main-Class': 'org.maplibre.mlt.cli.Encode'
decode.manifest.attributes 'Implementation-Title': 'decode an mlt',
        'Main-Class': 'org.maplibre.mlt.cli.Decode'

tasks.register('cli') {
    // Uncomment the following line to clear out the jars before rebuilding
    delete "${layout.buildDirectory}/libs"
    dependsOn encode
    dependsOn decode
}

plugins {
    id 'java-library'
    id('com.diffplug.spotless') version '8.2.0'
    id 'me.champeau.jmh' version '0.7.3'
    id 'jacoco'
    id 'com.vanniktech.maven.publish' version '0.36.0'
}

repositories {
    mavenCentral()
    maven {
        url 'https://maven.ecc.no/releases'
    }
}

dependencies {
    implementation 'com.carrotsearch:hppc:0.10.0'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'com.google.guava:guava:33.5.0-jre'
    implementation 'org.maplibre:earcut4j:3.0.0'
    implementation 'org.apache.commons:commons-lang3:3.20.0'
    runtimeOnly 'com.google.protobuf:protobuf-java:4.33.4'
    testImplementation 'io.github.sebasbaumh:mapbox-vector-tile-java:25.1.0'
    api 'me.lemire.integercompression:JavaFastPFOR:0.3.10'
    implementation 'no.ecc.vectortile:java-vector-tile:1.3.23'
    implementation 'org.locationtech.jts:jts-core:1.20.0'
    implementation 'org.slf4j:slf4j-simple:2.0.17'
    implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
    implementation 'org.jetbrains:annotations:26.0.2-1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:6.0.2'
    testImplementation 'org.openjdk.jmh:jmh-core:1.37'
    testImplementation 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
    testImplementation 'org.xerial:sqlite-jdbc:3.51.1.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:6.0.2'
}

test {
    jvmArgs "-Dcom.google.protobuf.use_unsafe_pre22_gencode"
    useJUnitPlatform()
    testLogging {
        outputs.upToDateWhen { false }
        showStandardStreams = true
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

spotless {
    java {
        importOrder()
        target 'src/*/java/**/*.java'
        googleJavaFormat()
        removeUnusedImports()
    }
}

tasks.register('compileWrapper', Exec) {
    workingDir = project.projectDir
    doFirst {
        if (System.properties['os.name'].toLowerCase().contains('windows')) {
            executable "../resources/compile-windows.bat"
        } else {
            executable "./compile-wrapper.sh"
        }
    }
}

// compileJava.dependsOn compileWrapper // Disabled due to CMake compatibility issues

gradle.projectsEvaluated {
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs << "-Xlint:unchecked"
            options.compilerArgs << "-Xlint:deprecation"
            options.compilerArgs << "-Xlint:all"
        }
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.register('validateSemver') {
    doLast {
        def version = project.version
        if (version == null || version == 'unspecified') {
            throw new GradleException("Version is not set. Please provide a version using -Pversion=<version>")
        }

        def semverPattern = /^\d+\.\d+\.\d+$/

        if (!version.matches(semverPattern)) {
            throw new GradleException("Version '$version' is not a valid semantic version. Expected format: major.minor.patch (e.g., 1.0.0, 2.1.3)")
        }

        println "âœ“ Version '$version' is valid semver"
    }
}

mavenPublishing {
    publishToMavenCentral(true)
    signAllPublications()

    coordinates("org.maplibre", "mlt", version)

    publishToMavenCentral.dependsOn validateSemver

    pom {
        name = "MapLibre Tile Specification"
        description = "Java implementation of the MapLibre Tile (MLT) specification"
        url = "https://github.com/maplibre/maplibre-tile-spec"

        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }

        developers {
            developer {
                id = "maplibre"
                name = "MapLibre contributors"
                url = "https://github.com/maplibre"
            }
        }

        scm {
            connection = "scm:git:git://github.com/maplibre/maplibre-tile-spec.git"
            developerConnection = "scm:git:ssh://github.com:maplibre/maplibre-tile-spec.git"
            url = "https://github.com/maplibre/maplibre-tile-spec"
        }
    }
}

// Disable signing for local publishing
tasks.withType(Sign) {
    onlyIf { !gradle.startParameter.taskNames.contains('publishMavenPublicationToMavenLocal') }
}

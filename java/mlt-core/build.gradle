plugins {
    alias(libs.plugins.diffplug.spotless)
    alias(libs.plugins.maven.publish)
    alias(libs.plugins.me.champeau.jmh)
    id 'jacoco'
    id 'java-library'
}

repositories {
    mavenCentral()
    maven {
        url = 'https://maven.ecc.no/releases'
    }
}

dependencies {
    api(libs.javaFastPFOR)
    implementation(libs.commons.lang3)
    implementation(libs.earcut4j)
    implementation(libs.gson)
    implementation(libs.guava)
    implementation(libs.hppc)
    implementation(libs.jakarta.annotation.api)
    implementation(libs.java.vector.tile)
    implementation(libs.jetbrains.annotations)
    implementation(libs.jts.core)
    implementation(libs.mapbox.vector.tile.java)
    implementation(libs.protobuf.java)

    testImplementation(libs.jmh.core)
    testImplementation(libs.jmh.generator.annprocess)
    testImplementation(libs.junit.jupiter.api)
    testImplementation(libs.junit.jupiter.params)
    testImplementation(libs.sqlite.jdbc)
    testRuntimeOnly(libs.junit.jupiter.engine)
    testRuntimeOnly(libs.junit.jupiter.platform.launcher)
}

test {
    jvmArgs "-Dcom.google.protobuf.use_unsafe_pre22_gencode"
    // Forward benchmark.iterations to the test JVM so tests can read it via Integer.getInteger()
    systemProperty 'benchmark.iterations', System.getProperty('benchmark.iterations', '1')
    useJUnitPlatform {
        excludeTags 'benchmark'
    }
    testLogging {
        outputs.upToDateWhen { false }
        showStandardStreams = true
        events "passed", "skipped", "failed"
        exceptionFormat = "full"
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = false
    }
    dependsOn test
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

spotless {
    java {
        importOrder()
        target 'src/*/java/**/*.java'
        googleJavaFormat()
        removeUnusedImports()
    }
}

tasks.register('compileWrapper', Exec) {
    workingDir = project.projectDir
    doFirst {
        if (System.properties['os.name'].toLowerCase().contains('windows')) {
            executable "../resources/compile-windows.bat"
        } else {
            executable "./compile-wrapper.sh"
        }
    }
}

// compileJava.dependsOn compileWrapper // Disabled due to CMake compatibility issues

gradle.projectsEvaluated {
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs << "-Xlint:unchecked"
            options.compilerArgs << "-Xlint:deprecation"
            options.compilerArgs << "-Xlint:all"
        }
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.register('validateSemver') {
    doLast {
        def version = project.version
        if (version == null || version == 'unspecified') {
            throw new GradleException("Version is not set. Please provide a version using -Pversion=<version>")
        }

        def semverPattern = /^\d+\.\d+\.\d+$/

        if (!version.matches(semverPattern)) {
            throw new GradleException("Version '$version' is not a valid semantic version. Expected format: major.minor.patch (e.g., 1.0.0, 2.1.3)")
        }

        println "âœ“ Version '$version' is valid semver"
    }
}

mavenPublishing {
    publishToMavenCentral(true)
    signAllPublications()

    coordinates("org.maplibre", "mlt", version)

    publishToMavenCentral.dependsOn validateSemver

    pom {
        name = "MapLibre Tile Specification"
        description = "Java implementation of the MapLibre Tile (MLT) specification"
        url = "https://github.com/maplibre/maplibre-tile-spec"

        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }

        developers {
            developer {
                id = "maplibre"
                name = "MapLibre contributors"
                url = "https://github.com/maplibre"
            }
        }

        scm {
            connection = "scm:git:git://github.com/maplibre/maplibre-tile-spec.git"
            developerConnection = "scm:git:ssh://github.com:maplibre/maplibre-tile-spec.git"
            url = "https://github.com/maplibre/maplibre-tile-spec"
        }
    }
}

// Disable signing for local publishing
tasks.withType(Sign) {
    onlyIf { !gradle.startParameter.taskNames.contains('publishMavenPublicationToMavenLocal') }
}
